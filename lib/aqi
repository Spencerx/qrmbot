#!/usr/bin/perl -w

# get air quality index data
#
# 2-clause BSD license.
# Copyright (c) 2023 molo1134@github. All rights reserved.
#

use strict;
use utf8;
use Encode qw(decode);
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");

use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;
use Location;
use Util;

use Math::Round;
use Time::Piece ();
use POSIX qw(floor);
use POSIX qw(strftime);
use JSON qw( from_json );

@ARGV = map { decode "utf-8", $_ } @ARGV;
# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);
# change this to 0 to always exit with success
our $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq getEggdropUID();

my $geo = undef;
my $query = undef;

my $i = 0;
while ($i <= $#ARGV) {
  if ($ARGV[$i] eq "about") {
    print "air quality index data from https://fire.airnow.gov/\n";
    exit $exitnonzeroonerror;
  }
  if (!defined($geo) and $ARGV[$i] =~ /^--geo$/i) {
    $i++;
    $geo = $ARGV[$i];
    $i++;
    next;
  }
  if (defined($query)) {
    $query = $query ." ". $ARGV[$i];
  } else {
    $query = $ARGV[$i];
  }
  $i++;
}

if (not defined $query and not defined $geo) {
  if ($username eq getEggdropUID()) {
    print "usage: !aqi <grid>|<lat>,<lon>|<qth>\n";
  } else {
    print "usage: $0 <grid>|<lat>,<lon>|<qth>\n";
  }
  exit $exitnonzeroonerror;
}

my $result = argToCoords($query) if defined $query;

if (defined $query and not defined $result) {
  print "error: unable to find \"$query\"\n";
  exit $exitnonzeroonerror;
}

my ($mylat, $mylon) = split(',', $geo) if defined $geo;
($mylat, $mylon) = split(',', $result) if defined $result;

#print "$mylat, $mylon\n";

my $cachedir = $ENV{HOME} . "/.qrmbot/cache";
my $oldcachefile = $cachedir . "/aqi.csv";

if (not -d $cachedir) {
  print "making cache directory..\n";
  system ("mkdir -p $cachedir\n");
  if (not -d $cachedir) {
    print "unable to make cache directory $cachedir\n";
  }
}

if (-e $oldcachefile) {
  unlink $oldcachefile or print "warning: unable to remove $oldcachefile\n";
}

our %stationData; # key is distance
getNearest();
displayStation();

sub getNearest {
  our %stationData;

  my $url = "https://near-me.airfire.org/fasm/monitor?lat=$mylat&lng=$mylon&maxDistanceMiles=10000&limit=1";
  #print "$url\n";

  open(JSON, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
  local $/;   # read entire file -- FIXME: potentially memory hungry

  binmode(JSON, ":utf8");
  my $json = <JSON>;
  close(JSON);
  my $j = from_json($json) or die "error: could not decode response\n";

  if (defined $j->{'aqMonitors'}->[0]) {

    my $sta = $j->{'aqMonitors'}->[0];
    my $ts = Time::Piece->strptime($sta->{'utc_ts'}, "%Y-%m-%dT%H:%M:%S+00:00")->epoch;
    my $src = "aq";
    my ($id, $epa_pm25, $epa_nowcast, $raw_pm25, $lat, $lng) = (
      $sta->{'unit_id'},
      "",
      defined($sta->{'nowcast'}) ? $sta->{'nowcast'} : "",
      $sta->{'raw_pm25'},
      $sta->{'lat'},
      $sta->{'lng'}
    );
    my ($dist, undef) = distBearing($mylat, $mylon, $lat, $lng);
    $stationData{$dist} = join('|', ($src, $id, $ts, $epa_pm25, $epa_nowcast, $raw_pm25, $lat, $lng));

  } else {
    print "warning: no aqMonitors found in getNearest()\n";
  }

  if (defined $j->{'purpleAir'}->[0]) {

    my $sta = $j->{'purpleAir'}->[0];
    my $ts = Time::Piece->strptime($sta->{'utc_ts'}, "%Y-%m-%dT%H:%M:%S+00:00")->epoch;
    my $src = "pa";
    my ($id, $epa_pm25, $epa_nowcast, $raw_pm25, $lat, $lng) = (
      $sta->{'unit_id'},
      $sta->{'corrected_pm25'},
      $sta->{'nowcast'},
      $sta->{'raw_pm25'},
      $sta->{'lat'},
      $sta->{'lng'}
    );
    #print "aqi: $sta->{'aqi'}\n" if defined $sta->{'aqi'}; # XXX DEBUG
    my ($dist, undef) = distBearing($mylat, $mylon, $lat, $lng);
    $stationData{$dist} = join('|', ($src, $id, $ts, $epa_pm25, $epa_nowcast, $raw_pm25, $lat, $lng));

  } else {
    print "warning: no purpleAir found in getNearest()\n";
  }

}

sub displayStation {
  our %stationData;
  my $key = (sort { $a <=> $b } keys %stationData)[0];

  if (not defined $key) {
    print "no stations found\n";
    return;
  }
  #print "$key => $stationData{$key}\n";

  my ($src, $id, $ts, $epa_pm25, $epa_nowcast, $raw_pm25, $lat, $lng) = split /\|/, $stationData{$key};
  my ($dist, $bearing) = distBearing($mylat, $mylon, $lat, $lng);
  my $grid = coordToGrid($lat, $lng);
  my $loc = geolocate($lat, $lng);

  #print "source: $src\n";

  my $now = time();
  my $old = "";
  $old = yellow(" [old]") if $ts < ($now - (60*60));  # 60 min
  $old = red(" [old]") if $ts < ($now - (120*60));    # 2 hour

  my $aqi = pm25_to_aqi($epa_nowcast);

  print "$loc ($grid): Air Quality Index (PM2.5): ",
      colorByAQI($aqi, "$aqi"),
      " (", descriptionByAQI($aqi), ")";
  print " at ", strftime("%Y-%m-%d %H%Mz", gmtime($ts)), $old;
  print " -- " , floor($dist), " km ", azToNEWS($bearing), " from ", coordToGrid($mylat, $mylon), "\n";

  my $series_ref;
  $series_ref = getTimeSeriesPA($id) if $src eq "pa";
  $series_ref = getTimeseriesAQ($id) if $src eq "aq";
  return if not defined $series_ref;

  my @result = seriesToSparkLine($series_ref);
  my $min = $result[0];
  my $max = $result[1];
  my @spark = @result[2 .. $#result];
  my $sparkline = optimizeIrcColor(join("", @spark));
  printf "  last 72 hours: %s - max %s\n", $sparkline, colorByAQI($max, $max);
}

sub colorByAQI {
  my $aqi = shift;
  my $text = shift;
  return $text if not defined $aqi;
  return red($text) if $aqi < 0;
  return green($text) if $aqi <= 50;
  return yellow($text) if $aqi <= 100;
  return darkYellow($text) if $aqi <= 150;
  return red($text) if $aqi <= 200;
  return magenta($text) if $aqi <= 300;
  return darkRed($text);
}

sub descriptionByAQI {
  my $aqi = shift;
  return red("error") if $aqi < 0;
  return green("Good") if $aqi <= 50;
  return yellow("Moderate") if $aqi <= 100;
  return darkYellow("Unhealthy for sensitive groups") if $aqi <= 150;
  return red("Unhealthy") if $aqi <= 200;
  return magenta("Very Unhealthy") if $aqi <= 300;
  return darkRed("Hazardous");
}

sub getTimeSeriesPA {
  my $id = shift;

  my $url = "https://s3-us-west-2.amazonaws.com/airfire-data-exports/maps/purple_air/v4/timeseries/weekly/$id.csv";
  #print "$url\n";
  open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
  local $/;   # read entire file -- FIXME: potentially memory hungry
  my $csv = <HTTP>;
  close(HTTP);

  if ($csv =~ m|<Error><Code>NoSuchKey</Code>|i) {
    return undef;
  }

  $csv =~ s/[^[:ascii:]]//g;

  my @series;

  foreach my $line (split /\n/, $csv) {
    next if $line =~ /^epa_pm25/; # skip header
    my ($epa_pm25, $epa_nowcast, $local_ts) = split /,/, $line;
    push @series, pm25_to_aqi($epa_nowcast)
      if defined $epa_nowcast and length($epa_nowcast) > 0;
  }

  my @result = @series[$#series-72 .. $#series];

  return \@result;
}

sub getTimeseriesAQ {
  my $id = shift;
  my $url = "https://s3-us-west-2.amazonaws.com/airfire-data-exports/monitoring/v2/latest/csv/$id.csv";
  #print "$url\n";
  open(HTTP, '-|', "curl --stderr - --max-time 10 -s -k -L '$url'");
  local $/;   # read entire file -- FIXME: potentially memory hungry
  my $csv = <HTTP>;
  close(HTTP);

  if ($csv =~ m|<Error><Code>NoSuchKey</Code>|i) {
    return undef;
  }

  $csv =~ s/[^[:ascii:]]//g;

  my @series;

  foreach my $line (split /\n/, $csv) {
    next if $line =~ /^utcTime/; # skip header
    my ($utcTime, $localTime, $pm25, $nowcast) = split /,/, $line;
    push @series, pm25_to_aqi($nowcast)
      if defined $nowcast and length($nowcast) > 0;
  }

  my @result = @series[$#series-72 .. $#series];

  return \@result;
}

sub seriesToSparkLine {
  my $arrayref = shift;
  my @series = @$arrayref;
  my $min = 999999999999;
  my $max = -999999999999;
  my @result;

  foreach my $e (@series) {
    $min = $e if defined $e and $e < $min and $e > 0;
    $max = $e if defined $e and $e > $max and $e > 0;
  }
  $max = 0 if $max == -999999999999;

  push @result, ($min, $max);

  foreach my $v (@series) {
    my $chr = valToBlock($v, $min, $max);
    $chr = colorByAQI($v, $chr);
    push @result, $chr;
  }

  return @result;
}

# relative to min/max
sub valToBlock {
  my $v = shift;
  my $min = shift;
  my $max = shift;
  my $step = ($max - $min)/8;
  my $chr = " ";

  return $chr if not defined $v;

  if ($v < $min) {
    $chr = "\xA0"; # NBSP
  } elsif ($v <= ($min + ($step * 1))) {
    $chr = "▁";
  } elsif ($v <= ($min + ($step * 2))) {
    $chr = "▂";
  } elsif ($v <= ($min + ($step * 3))) {
    $chr = "▃";
  } elsif ($v <= ($min + ($step * 4))) {
    $chr = "▄";
  } elsif ($v <= ($min + ($step * 5))) {
    $chr = "▅";
  } elsif ($v <= ($min + ($step * 6))) {
    $chr = "▆";
  } elsif ($v <= ($min + ($step * 7))) {
    $chr = "▇";
  } elsif ($v <= ($min + ($step * 8))) {
    $chr = "█";
  } else {
    $chr = "↑";
  }
  return $chr;
}

sub pm25_to_aqi {
  # https://forum.airnowtech.org/t/the-aqi-equation/169
  my $pm25 = shift;
  return -1 if not isNumeric($pm25);

  my ($aqi_lo, $aqi_hi, $conc_lo, $conc_hi);

  if ($pm25 < 12.1) {
    $aqi_lo = 0.0;
    $aqi_hi = 50.0;
    $conc_lo = 0.0;
    $conc_hi = 12.0;
  } elsif ($pm25 < 35.5) {
    $aqi_lo = 51.0;
    $aqi_hi = 100.0;
    $conc_lo = 12.1;
    $conc_hi = 35.4;
  } elsif ($pm25 < 55.5) {
    $aqi_lo = 101.0;
    $aqi_hi = 150.0;
    $conc_lo = 35.5;
    $conc_hi = 55.4;
  } elsif ($pm25 < 150.5) {
    $aqi_lo = 151.0;
    $aqi_hi = 200.0;
    $conc_lo = 55.5;
    $conc_hi = 150.4;
  } elsif ($pm25 < 250.5) {
    $aqi_lo = 201.0;
    $aqi_hi = 300.0;
    $conc_lo = 150.5;
    $conc_hi = 250.4;
  } elsif ($pm25 < 500.5) {
    $aqi_lo = 301.0;
    $aqi_hi = 500.0;
    $conc_lo = 250.5;
    $conc_hi = 500.4;
  } else {
    return -1;
  }

  my $aqi = ((($aqi_hi - $aqi_lo) / ($conc_hi - $conc_lo)) * ($pm25 - $conc_lo)) + $aqi_lo;

  return round($aqi);
}
