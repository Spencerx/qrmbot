#!/usr/bin/perl -w
#
# Pull info from FCC database
#
# 2-clause BSD license.
# Copyright (c) 2019, 2020, 2021 molo1134@github. All rights reserved.

use strict;
use utf8;
use feature 'unicode_strings';
binmode(STDOUT, ":utf8");


use File::Basename;
use Cwd 'realpath';
use lib dirname(realpath(__FILE__));
use Colors;
use Util;

use Time::Local;
use Time::Piece ();
use List::Util qw[min max];
use POSIX qw(strftime);

# disable "experimental" warning on smart match operator use
no if $] >= 5.018, warnings => "experimental::smartmatch";

my $username = $ENV{'USER'} || $ENV{'USERNAME'} || getpwuid($<);
my $highlight="vt220";
$highlight = "none" if ! -t STDOUT;
$highlight = "irc" if $username eq getEggdropUID();

my $predictionBaseUrl = "https://vanities.k2cr.com/";

# change this to 0 to always exit with success
our $exitnonzeroonerror = 1;
$exitnonzeroonerror = 0 if $username eq getEggdropUID();

# eggdrop doesn't split args on spaces
@ARGV=split(' ', join(' ',@ARGV));
@ARGV = uniq(@ARGV);	# deduplicate - no reason to process the same url twice

if ($#ARGV < 0 || length($ARGV[0]) == 0) {
  if ($username eq getEggdropUID()) {
    print "usage: !vanity <call>|<region/AK/HI/territory>\n";
  } else {
    print "usage: $0 <call>|<region/AK/HI/territory>\n";
  }
  exit $exitnonzeroonerror;
}

my $httpTimeout = 5;
my $i = 0;
while ($i <= $#ARGV) {

  $ARGV[$i] =~ s/'//g; # sanitize

  if ($ARGV[$i] =~ m/^[0-9]+$/) {
    if ($ARGV[$i] == 12) {
      doRegion("12_PR");
      doRegion("12_VI");
    } elsif ($ARGV[$i] == 13) {
      doRegion("13_HI");
      doRegion("13_GU");
      doRegion("13_AS");
      doRegion("13_MP");
    } elsif ($ARGV[$i] > 13 and length($ARGV[$i]) == 10) {
      doApp($ARGV[$i]);
    } elsif ($ARGV[$i] > 13) {
      print "invalid region\n";
      exit $exitnonzeroonerror;
    } else {
      doRegion($ARGV[$i]);
    }
  } else {
    if ($ARGV[$i] eq "AK") {
      doRegion(11);
    } elsif ($ARGV[$i] eq "HI") {
      doRegion("13_HI");
    } elsif ($ARGV[$i] eq "GU") {
      doRegion("13_GU");
    } elsif ($ARGV[$i] eq "AS") {
      doRegion("13_AS");
    } elsif ($ARGV[$i] eq "MP") {
      doRegion("13_MP");
    } elsif ($ARGV[$i] eq "PR") {
      doRegion("12_PR");
    } elsif ($ARGV[$i] eq "VI") {
      doRegion("12_VI");
    } else {
      doCall($ARGV[$i]);
    }
  }
  $i++;
}

sub doRegion {
  my $region = shift;
  my $saved = $_;
  $region = 10 if isNumeric($region) and $region == 0;
  my $url = "$predictionBaseUrl/shortcalls-$region.csv";
  
  my @avail;
  my @pending;
  my @upcoming;
  my @unavail;
  my %regionPreds;
  my %regionComp;
  my %prelim;
  open(HTTP, '-|', "curl --max-time $httpTimeout -L -k -s '$url'");
  binmode(HTTP, ":utf8");
  while(<HTTP>) {
    chomp;
    next if /^#/;
    my ($call, $status, $odds, $comp) = split /,/;
    push @avail, $call if $status =~ /^available/;
    push @unavail, $call if $status =~ /^blocked/;
    push @unavail, $call if $status =~ /^blacklisted/;
    push @upcoming, $call if $status =~ /^upcoming/;
    push @pending, $call if $status =~ /^pending/;
    $prelim{$call} = 1 if $status =~ /†/;
    $regionPreds{$call} = $odds if $status =~ /^pending/;
    $regionComp{$call} = $comp if defined $comp and $comp ne "";
  }
  close(HTTP);

  print "Region $region: ", green(scalar @avail), " available";

  if (scalar @avail > 0) {
    #print ": ", green(join(", ", @avail));
    my $num = 0;
    print ": ";
    foreach my $c (@avail) {
      print green($c);
      print yellow(" ($regionPreds{$c}%)") # \
	if defined $regionPreds{$c} and $regionPreds{$c} > 0.0;
      print red("†") if $prelim{$c};
      if ($num >= 30) {
	print " (truncated)";
	last;
      }
      print ", " unless ++$num == scalar @avail;
    }
  }

  print "; ", lightblue(scalar @pending), " pending";

  if (scalar @pending > 0) {
    my $num = 0;
    print ": ";
    foreach my $c (@pending) {
      print lightblue($c);
      my $dopct = (defined $regionPreds{$c} and $regionPreds{$c} < 99.5); # allow for rounding
      my $docomp = (defined $regionComp{$c} and $regionComp{$c} > 1);
      if ($dopct or $docomp) {
	  print " (";
	  print yellow("$regionPreds{$c}%") if $dopct;
	  print " " if $dopct and $docomp;
	  print "$regionComp{$c}x" if $docomp;
	  print ")";
      }
      print red("†") if $prelim{$c};
      print ", " unless ++$num == scalar @pending;
    }
    #print ": ", lightblue(join(", ", @pending)) if scalar @pending > 0;
  }

  print "; ", yellow(scalar @upcoming), " upcoming";
  print ": ", yellow(join(", ", @upcoming)) if scalar @upcoming > 0;
  print "; ", red(scalar @unavail), " unavailable: ",
	red(join(", ", @unavail)) if scalar @unavail > 0;
  print "\n";
  $_ = $saved;
}

sub doCall {
  my $call = uc shift;
  my $saved = $_;

  my ($frn, $name, $grant, $eff, $cxl, $expire);
  my ($receipt, $process, $appl, $filenum, $type, $seq, $pred);
  my $callStatus;
  my $found = 0;
  my @frns;
  my %odds_sim;

  my @tooearly;
  my @toolate;

  my ($status, $avail, $availdate, $chance, $class, $region, $restricted);
  my $url = "${predictionBaseUrl}/vanity-$call.csv";
  open(CSV, '-|', "curl --fail --max-time $httpTimeout -L -k -s '$url'");
  binmode(CSV, ":utf8");
  while(<CSV>) {
    chomp;
    $status     = $1 if /^# status: (.*)$/;
    $avail      = $1 if /^# avail: (.*)$/;
    $chance     = $1 if /^# chance: (.*)$/;
    $availdate  = $1 if /^# availdate: (.*)$/;
    $class      = $1 if /^# class: (.*)$/;
    $region     = $1 if /^# region: (.*)$/;
    $restricted = $1 if /^# restricted: (.*)$/;
    if (/^#metadata_end/) {
      print bold($call), ": ", formatStatus($status);
      print yellow(sprintf(" (%0.1f%%)", $chance)) if defined $chance and $chance ne "";
      print green(" (available)") if $avail;
      print red(" (not available)") if not $avail;
      print ": Req'd class: $class -- Avail: $availdate -- $region";
      print bold(" (restricted)") if $restricted;
      print "\n";
      ($status, $class) = (undef, undef);
    }
    next if /^#/;
    my ($appid, $rdate, $pdate, $appcall, $state, $seq, $vanity, $pred, $odds) = split /,/;
    if ($pred =~ /^Too Early/i) {
      push @tooearly, $appcall;
    } elsif ($pred =~ /^Too Late/i) {
      push @toolate, $appcall;
    } else {
      $odds = formatOdds($odds);
      $pred = formatPred($pred);
      $pdate = " ?        " if $pdate eq "";
      $seq = "$seq/?";		# TODO FIXME
      printf "$rdate -> $pdate %-6s ($state) App $appid #%-5s $pred%s\n", $appcall, $seq, $odds;
    }
    $found = 1;
  }
  close(CSV);
  if (scalar @tooearly > 0 or scalar @toolate > 0) {
    print red("Too Early"), ": ", join (", ", @tooearly) if scalar @tooearly > 0;
    print "; " if scalar @tooearly > 0 and scalar @toolate > 0;
    print red("Too Late"), ": ", join (", ", @toolate) if scalar @toolate > 0;
    print "\n";
  }

  $url = "${predictionBaseUrl}/call-$call.csv";
  my ($grantdate, $expdate, $cxldate, $state, $district);
  open(CSV, '-|', "curl --fail --max-time $httpTimeout -L -k -s '$url'");
  binmode(CSV, ":utf8");
  while(<CSV>) {
    chomp;
    $status    = $1 if /^# status: (.*)$/;
    $grantdate = $1 if /^# grantdate: (.*)$/;
    $expdate   = $1 if /^# expdate: (.*)$/;
    $cxldate   = $1 if /^# cxldate: (.*)$/;
    $class     = $1 if /^# class: (.*)$/;
    $state     = $1 if /^# state: (.*)$/;
    $district  = $1 if /^# district: (.*)$/;
    if (/^#metadata_end/) {
      print bold($call), ": ", formatStatus($status);
      print ": class: $class -- grant: $grantdate -- exp: $expdate ";
      print "-- cxl: $cxldate " if defined $cxldate and $cxldate ne "";
      print "-- $state ($district)\n"
    }
    next if /^#/;
    my ($appid, $rdate, $pdate, $call, $state, $seq, $vanity, $pred, $odds) = split /,/;
    $pdate = " ?        " if $pdate eq "";
    my $type = "type?";		# TODO FIXME
    $odds = formatOdds($odds);
    $pred = formatPred($pred);
    print "$rdate -> $pdate App $appid ($type) #$seq $vanity $pred$odds\n";
    $found = 1;
  }
  close(CSV);

  print "no pending applications found matching $call\n" if $found == 0;
#  print bold($call), ": $callStatus\n" if defined $callStatus and not $found;
  $_ = $saved;
}

sub doApp {
  my $app = shift;
  my $url = "${predictionBaseUrl}/app-$app.csv";
  open(CSV, '-|', "curl --fail --max-time $httpTimeout -L -k -s '$url'");
  binmode(CSV, ":utf8");
  while(<CSV>) {
    chomp;
    next if /^#/;
    my ($appid, $rdate, $pdate, $call, undef, $seq, $vanity, $pred, $odds) = split /,/;
    $pdate = " ?        " if $pdate eq "";
    my $type = "type?";		# TODO FIXME
    $odds = formatOdds($odds);
    $pred = formatPred($pred);
    print "$rdate -> $pdate App $appid ($type) #$seq $vanity $pred$odds\n";
  }
  close(CSV);
}

sub uniq {
  my %seen;
  grep !$seen{$_}++, @_;
}

sub formatOdds {
  my $odds = shift;
  if (not defined $odds or length($odds) == 0) {
    $odds = "";
  } else {
    if (isNumeric($odds) and $odds == 100.0) {
      $odds = green(" 100%");
    } elsif (isNumeric($odds) and $odds == 0.0) {
      $odds = red(" 0%");
    } else {
      $odds = yellow(" $odds%");
    }
  }
  return $odds;
}

sub formatPred {
  my $pred = shift;
  my $prelim = $pred =~ tr/†//;
  $pred =~ s/_/ /g;
  $pred =~ s/†//g;
  $pred = green($pred) if $pred =~ /^Assignment/;
  $pred = lightblue($pred) if $pred =~ /^(Competition|Available)/;
  $pred = italic($pred) if $pred =~ /^Unneeded/;
  $pred = yellow($pred) if $pred =~ /^(Offlined|Await)/;
  $pred = red($pred) if $pred =~ /^(Too|Not|Invalid|Insuff|Restricted|Active|Duplic|Reserv|Inactive|Block|Black)/;
  $pred .= red("†") if $prelim;
  return $pred;
}


sub formatStatus {
  my $status = shift;
  my $ret = undef;

  my %h_status;
  $h_status{"A"} = "Active";
  $h_status{"C"} = "Canceled";
  $h_status{"E"} = "Expired";
  $h_status{"T"} = "Terminated";

  $ret = green($h_status{$status} || $status) if $status =~ /^A/;
  $ret = yellow($h_status{$status} || $status) if $status =~ /^C/ or $status =~ /^E/;
  $ret = red($h_status{$status} || $status) if $status =~ /^T/;
  $ret = lightblue($status) if $status =~ /Pending/;
  $ret = bold($status) if not defined $ret;
  return $ret;
}
